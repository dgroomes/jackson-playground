plugins {
    id "java"
    id "application"
}

ext {
    slf4jVersion = "1.7.26"
    jacksonVersion = "2.10.1"
    junitPlatformVersion = "1.5.2"
    junitJupiterVersion = "5.5.1"
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
}

/**
 * Configure the compiler step to accommodate:
 * - Java language preview features
 * - Preserve parameter names in the bytecode to enable Jackson to deserialize using constructors
 */
tasks.withType(JavaCompile) {
    options.compilerArgs += ["--enable-preview", "-parameters"]
}

tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs = ["--enable-preview"]
    testLogging {
        events = ["started", "skipped", "failed", "standardError"] //, "standardOut"
        exceptionFormat = "full"
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}

application {
    mainClassName = "dgroomes.Runner"
}
