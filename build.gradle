plugins {
    id "java"
    id "application"
}

ext {
    slf4jVersion = "1.7.30" // releases: http://www.slf4j.org/news.html
    jacksonVersion = "2.12.0-SNAPSHOT" // these artifacts must be built from source. see the README.md
    junitPlatformVersion = "1.7.0" // releases: https://github.com/junit-team/junit5/releases
    junitJupiterVersion = "5.7.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
}

/**
 * Configure the compiler step to accommodate:
 * - Java language preview features
 * - Preserve parameter names in the bytecode to enable Jackson to deserialize using constructors
 */
tasks.withType(JavaCompile) {
    options.compilerArgs += ["--enable-preview", "-parameters"]
}

tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs = ["--enable-preview"]
    testLogging {
        events = ["started", "skipped", "failed", "standardError"] //, "standardOut"
        exceptionFormat = "full"
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"

    implementation(platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion"))
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("com.fasterxml.jackson.module:jackson-module-parameter-names")

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}

application {
    mainClassName = "dgroomes.Runner"
}
